plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.7.0'
    id 'org.jetbrains.grammarkit' version '2021.2.2'
}

group 'com.github.vitalibo'
version (project.hasProperty('version') ? project.getProperty('version') : '1.1-SNAPSHOT')

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.testng:testng:7.6.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

sourceSets.main.java.srcDirs 'src/main/gen'

intellij {
    version = '2022.1.3'
    pluginName = 'BitBake'
    updateSinceUntilBuild = false
}

tasks {

    tasks.withType(JavaCompile) {
        sourceCompatibility = "11"
        targetCompatibility = "11"

        dependsOn generateLexer
        dependsOn generateParser
    }

    patchPluginXml {
        sinceBuild = '213'
        pluginDescription = """Highlight .bb/.bbappend/.bbclass/.inc files in JetBrains IDEs to make working with
                               BitBake-based projects (like anything based on Yocto Project) easier."""
        changeNotes = """Fix compatibility with IDEA/PyCharm Ultimate."""
    }

    generateLexer {
        source = "src/main/java/com/github/vitalibo/intellij/bitbake/BitBake.flex"
        targetDir = 'src/main/gen/com/github/vitalibo/intellij/bitbake'
        targetClass = "BitBakeLexer"
        purgeOldFiles = true
    }

    generateParser {
        source = "src/main/java/com/github/vitalibo/intellij/bitbake/BitBake.bnf"
        targetRoot = 'src/main/gen'
        pathToParser = '/com/github/vitalibo/intellij/bitbake/parser/BitBakeParser.java'
        pathToPsiRoot = '/com/github/vitalibo/intellij/bitbake/psi'
        purgeOldFiles = true
    }

    buildSearchableOptions {
        enabled = false
    }

}
